>Modifiable = package unsafe bootstrap Modifiable

Class {
	:this for {{
		>cb = .
		>loop = {
			. {{
				cb {... .}
				loop .
			}}
		}
		
		loop :
	}}
	
	:this backing {:}
	
	:this first {:{{..}}}
	:this rest {Iterator :{{.}}}
	:this empty {:{{false}}; true}
	
	:this last {
		>loop = {
			. {{
				loop .
				..
			}}
		}
		
		loop :
	}
	
	:this reverse {
		>current = Modifiable ()
		
		:this for {
			>cur = current get
			
			current set {. .. (cur)}
		}
		
		Iterator (current get)
	}
	
	:this zip {{
		>zip = {{
			..empty & .empty !? {
				{. [...first; ..first] (zip ...rest ..rest)}
			}
		}}
		
		Iterator (zip :this .)
	}}
	
	:this + {{
		>add = .backing
		>walk = {
			. {{
				{. ... (walk ..)}
			}}
			
			add
		}
		
		Iterator (walk :)
	}}
	
	:this map {{
		>fn = .
		
		>walk = {
			. {{
				{
					. (fn ...) (walk ..)
				}
			}}
		}
		
		Iterator (walk :) 
	}}
	
	:this filter {{
		>fn = .
		
		>walk = {
			. {{
				fn .. ? {
					{. ... (walk ..)}
				}
				
				walk .
			}}
		}
		
		Iterator (walk :)
	}}
	
	:this length {
		>loop = {
			. {{
				1 + loop .
			}}
			
			0
		}
		
		loop :
	}
	
	:this array {
		Array :this
	}
	
	:this clone {
		>walk = {
			. {{
				>next = walk .
				>value = ..
				
				{. value next}
			}}
		}
		
		Iterator (walk :)
	}
}